unit Model.Despesas;

interface

uses
  FireDAC.Comp.Client, Data.DB, System.SysUtils, model.connection,
  System.StrUtils, FireDAC.DApt, System.Generics.Collections, System.JSON;

type
  { ===== Entidade de Rateio ===== }
  TDespesaRateio = class
  private
    Fid: Integer;
    Fiddespesas: Integer;    // ATENÇÃO: aqui guardaremos o idgeral (para bater com a FK)
    Ftipo: string;           // 'DEPARTAMENTO' | 'SITE'
    Fdepartamento: string;   // quando tipo = DEPARTAMENTO
    Fidsite: string;         // quando tipo = SITE
    Fpercentual: Double;
  public
    property id: Integer read Fid write Fid;
    property iddespesas: Integer read Fiddespesas write Fiddespesas; // idgeral
    property tipo: string read Ftipo write Ftipo;
    property departamento: string read Fdepartamento write Fdepartamento;
    property idsite: string read Fidsite write Fidsite;
    property percentual: Double read Fpercentual write Fpercentual;
  end;

  { ===== Repository/Model de Despesas ===== }
  TDespesas = class
  private
    FConn: TFDConnection;
    Fiddespesas: Integer;
    Fdatalancamento: string;
    Fvalordespesa: string;
    Fdescricao: string;
    Fcomprovante: string;
    Fobservacao: string;
    Fidcliente: Integer;
    Fidloja: Integer;
    Fdeletado: Integer;
    Fidempresa: integer;
    Fidpessoa: Integer;
    Fidveiculo: integer;
    Ffuncionario: string;
    Fcategoria: string;
    Fperiodicidade: string;
    Fperiodo: string;
    Fparceladoem: string;
    Fvalordaparcela: string;
    Fdespesacadastradapor: string;
    FdataInicio: string;

    { ===== Suporte ao Rateio ===== }
    FRateios: TObjectList<TDespesaRateio>;

    function BuscarIdGeralAtual: Integer;
    procedure SalvarRateiosOrThrow(const AIdGeral: Integer);
    procedure CarregarRateiosIntoList(const AIdGeral: Integer);

  public
    constructor Create;
    destructor Destroy; override;

    property iddespesas: Integer read Fiddespesas write Fiddespesas;
    property idcliente: Integer read Fidcliente write Fidcliente;
    property idloja: Integer read Fidloja write Fidloja;
    property deletado: Integer read Fdeletado write Fdeletado;
    property funcionario: string read Ffuncionario write Ffuncionario;
    property periodicidade: string read Fperiodicidade write Fperiodicidade;
    property categoria: string read Fcategoria write Fcategoria;
    property datalancamento: string read Fdatalancamento write Fdatalancamento;
    property valordespesa: string read Fvalordespesa write Fvalordespesa;
    property descricao: string read Fdescricao write Fdescricao;
    property periodo: string read Fperiodo write Fperiodo;
    property valordaparcela: string read Fvalordaparcela write Fvalordaparcela;
    property comprovante: string read Fcomprovante write Fcomprovante;
    property observacao: string read Fobservacao write Fobservacao;
    property idempresa: integer read Fidempresa write Fidempresa;
    property idpessoa: integer read Fidpessoa write Fidpessoa;
    property idveiculo: integer read Fidveiculo write Fidveiculo;
    property dataInicio: string read FdataInicio write FdataInicio;
    property parceladoEm: string read FparceladoEm write FparceladoEm;
    property despesacadastradapor: string read Fdespesacadastradapor write Fdespesacadastradapor;

    { ===== Acesso aos Rateios ===== }
    property Rateios: TObjectList<TDespesaRateio> read FRateios;
    procedure ClearRateios;
    procedure AddRateio_Departamento(const ANomeDepto: string; APercentual: Double);
    procedure AddRateio_Site(const AIdSite: string; APercentual: Double);
    procedure LoadRateiosFromJSONArray(const AJSONArray: TJSONArray);
    function CarregarRateios: Boolean; // usa iddespesas -> resolve idgeral -> popula FRateios

    { ===== API legado que o controller usa ===== }
    function Lista(const AQuery: TDictionary<string, string>; out erro: string): TFDQuery;
    function Listaid(const AQuery: TDictionary<string, string>; out erro: string): TFDQuery;
    function Editar(out erro: string): Boolean;
    function NovoCadastro(out erro: string): integer;
  end;

implementation

{ TDespesas  }

constructor TDespesas.Create;
begin
  FConn := TConnection.CreateConnection;
  FRateios := TObjectList<TDespesaRateio>.Create(True); // True => OwnsObjects
end;

destructor TDespesas.Destroy;
begin
  if Assigned(FRateios) then
    FRateios.Free;
  if Assigned(FConn) then
    FConn.Free;
  inherited;
end;

procedure TDespesas.ClearRateios;
begin
  FRateios.Clear;
end;

procedure TDespesas.AddRateio_Departamento(const ANomeDepto: string; APercentual: Double);
var
  R: TDespesaRateio;
begin
  R := TDespesaRateio.Create;
  R.id := 0;
  R.iddespesas := 0;  // será atribuído com idgeral na hora de salvar
  R.tipo := 'DEPARTAMENTO';
  R.departamento := ANomeDepto;
  R.idsite := '';
  R.percentual := APercentual;
  FRateios.Add(R);
end;

procedure TDespesas.AddRateio_Site(const AIdSite: string; APercentual: Double);
var
  R: TDespesaRateio;
begin
  R := TDespesaRateio.Create;
  R.id := 0;
  R.iddespesas := 0;  // será atribuído com idgeral na hora de salvar
  R.tipo := 'SITE';
  R.departamento := '';
  R.idsite := AIdSite;
  R.percentual := APercentual;
  FRateios.Add(R);
end;

procedure TDespesas.LoadRateiosFromJSONArray(const AJSONArray: TJSONArray);
var
  I: Integer;
  Obj: TJSONObject;
  Tipo, Depto, Site: string;
  Perc: Double;
begin
  if not Assigned(AJSONArray) then Exit;
  ClearRateios;
  for I := 0 to AJSONArray.Count - 1 do
  begin
    if not (AJSONArray.Items[I] is TJSONObject) then Continue;
    Obj := TJSONObject(AJSONArray.Items[I]);

    Tipo := Obj.GetValue<string>('tipo', '');
    Depto := Obj.GetValue<string>('departamento', '');
    Site := Obj.GetValue<string>('idsite', '');
    Perc := Obj.GetValue<Double>('percentual', 0);

    if SameText(Tipo, 'DEPARTAMENTO') and (Depto <> '') and (Perc > 0) then
      AddRateio_Departamento(Depto, Perc)
    else
    if SameText(Tipo, 'SITE') and (Site <> '') and (Perc > 0) then
      AddRateio_Site(Site, Perc);
  end;
end;

function TDespesas.BuscarIdGeralAtual: Integer;
var
  Q: TFDQuery;
begin
  Result := 0;
  Q := TFDQuery.Create(nil);
  try
    Q.Connection := FConn;
    Q.SQL.Text :=
      'SELECT idgeral FROM gesdespesas '+
      'WHERE idcliente = :idcliente AND idloja = :idloja AND iddespesas = :iddespesas';
    Q.ParamByName('idcliente').AsInteger := idcliente;
    Q.ParamByName('idloja').AsInteger := idloja;
    Q.ParamByName('iddespesas').AsInteger := iddespesas;
    Q.Open;
    if not Q.IsEmpty then
      Result := Q.FieldByName('idgeral').AsInteger;
  finally
    Q.Free;
  end;
end;

procedure TDespesas.SalvarRateiosOrThrow(const AIdGeral: Integer);
var
  qry: TFDQuery;
  r: TDespesaRateio;
begin
  qry := TFDQuery.Create(nil);
  try
    qry.Connection := FConn;

    // limpa rateios antigos desta despesa (por idgeral)
    qry.SQL.Text := 'DELETE FROM gesdespesas_rateio WHERE iddespesas = :id';
    qry.ParamByName('id').AsInteger := AIdGeral;
    qry.ExecSQL;

    // insere os novos
    for r in FRateios do
    begin
      qry.SQL.Text :=
        'INSERT INTO gesdespesas_rateio (iddespesas, tipo, departamento, idsite, percentual) ' +
        'VALUES (:iddespesas, :tipo, :departamento, :idsite, :percentual)';
      qry.ParamByName('iddespesas').AsInteger := AIdGeral;
      qry.ParamByName('tipo').AsString := r.tipo;

      if SameText(r.tipo, 'DEPARTAMENTO') then
      begin
        qry.ParamByName('departamento').AsString := r.departamento;
        qry.ParamByName('idsite').Clear;
      end
      else
      begin
        qry.ParamByName('departamento').Clear;
        qry.ParamByName('idsite').AsString := r.idsite;
      end;

      qry.ParamByName('percentual').AsFloat := r.percentual;
      qry.ExecSQL;
    end;

  finally
    qry.Free;
  end;
end;

procedure TDespesas.CarregarRateiosIntoList(const AIdGeral: Integer);
var
  qry: TFDQuery;
  r: TDespesaRateio;
begin
  FRateios.Clear;

  qry := TFDQuery.Create(nil);
  try
    qry.Connection := FConn;
    qry.SQL.Text :=
      'SELECT id, iddespesas, tipo, departamento, idsite, percentual ' +
      'FROM gesdespesas_rateio WHERE iddespesas = :id';
    qry.ParamByName('id').AsInteger := AIdGeral;
    qry.Open;

    while not qry.Eof do
    begin
      r := TDespesaRateio.Create;
      r.id := qry.FieldByName('id').AsInteger;
      r.iddespesas := qry.FieldByName('iddespesas').AsInteger; // aqui é idgeral
      r.tipo := qry.FieldByName('tipo').AsString;
      r.departamento := qry.FieldByName('departamento').AsString;
      r.idsite := qry.FieldByName('idsite').AsString;
      r.percentual := qry.FieldByName('percentual').AsFloat;
      FRateios.Add(r);
      qry.Next;
    end;
  finally
    qry.Free;
  end;
end;

function TDespesas.CarregarRateios: Boolean;
var
  LIdGeral: Integer;
begin
  LIdGeral := BuscarIdGeralAtual;
  Result := LIdGeral > 0;
  if Result then
    CarregarRateiosIntoList(LIdGeral)
  else
    FRateios.Clear;
end;

function TDespesas.NovoCadastro(out erro: string): integer;
var
  qry: TFDQuery;
begin
  try
    qry := TFDQuery.Create(nil);
    qry.connection := FConn;
    try
      FConn.StartTransaction;
      with qry do
      begin
        Active := false;
        sql.Clear;
        sql.add('update admponteiro set iddespesas = iddespesas+1 where idcliente=:idcliente and idloja=:idloja ');
        ParamByName('idcliente').asinteger := idcliente;
        ParamByName('idloja').AsInteger := idloja;
        execsql;
        close;
        sql.Clear;
        sql.add('select iddespesas from admponteiro where idcliente=:idcliente and idloja=:idloja ');
        ParamByName('idcliente').asinteger := idcliente;
        ParamByName('idloja').AsInteger := idloja;
        Open;
        iddespesas := fieldbyname('iddespesas').AsInteger;
      end;
      FConn.Commit;
      erro := '';
      Result := iddespesas;
    except
      on ex: exception do
      begin
        FConn.Rollback;
        erro := 'Erro ao consultar : ' + ex.Message;
        Result := 0;
      end;
    end;
  finally
    qry.Free;
  end;
end;

function TDespesas.Editar(out erro: string): Boolean;
var
  qry, qHist, qInsert: TFDQuery;
  IsInsert: Boolean;
  LIdGeral: Integer;
begin
  try
    qry := TFDQuery.Create(nil);
    qry.connection := FConn;
    try
      FConn.StartTransaction;
      with qry do
      begin
        Active := false;
        sql.Clear;
        sql.add('select iddespesas from gesdespesas where idcliente=:idcliente and idloja=:idloja and iddespesas=:iddespesas ');
        ParamByName('idcliente').asinteger := idcliente;
        ParamByName('idloja').AsInteger := idloja;
        ParamByName('iddespesas').Value := iddespesas;
        Open;
        IsInsert := (RecordCount = 0);
        Close;
        if IsInsert then
        begin
          Active := false;
          sql.Clear;
          SQL.Add('INSERT INTO gesdespesas(iddespesas, datalancamento, parceladoem, datainicio, valorparcela, despesacadastradapor, ');
          SQL.Add('valordespesa, descricao, comprovante, observacao, ');
          SQL.Add('idempresa, idpessoa, idveiculo, deletado, idcliente, idloja, ');
          SQL.Add('periodo, periodicidade, categoria, datadocadastro) ');
          SQL.Add('VALUES(:iddespesas, :datalancamento, :parceladoem, :datainicio, :valorparcela, :despesacadastradapor, ');
          SQL.Add(':valordespesa, :descricao, :comprovante, :observacao, ');
          SQL.Add(':idempresa, :idpessoa, :idveiculo, :deletado, :idcliente, :idloja, ');
          SQL.Add(':periodo, :periodicidade, :categoria, :datadocadastro)');
          Params.ParamByName('datadocadastro').AsDateTime := Now;

        end
        else
        begin
          Active := false;
          sql.Clear;
          SQL.Add('UPDATE gesdespesas SET ');
          SQL.Add('DELETADO = :DELETADO, ');
          SQL.Add('parceladoem = :parceladoem, ');
          SQL.Add('datainicio = :datainicio, ');
          SQL.Add('valorparcela = :valorparcela, ');
          SQL.Add('despesacadastradapor = :despesacadastradapor, ');
          SQL.Add('datalancamento = :datalancamento, ');
          SQL.Add('valordespesa = :valordespesa, ');
          SQL.Add('descricao = :descricao, ');
          SQL.Add('comprovante = :comprovante, ');
          SQL.Add('categoria = :categoria, ');
          SQL.Add('periodicidade = :periodicidade, ');
          SQL.Add('periodo = :periodo, ');
          SQL.Add('observacao = :observacao, ');
          SQL.Add('idempresa = :idempresa, ');
          SQL.Add('idpessoa = :idpessoa, ');
          SQL.Add('idveiculo = :idveiculo ');
          SQL.Add('WHERE idcliente = :idcliente AND idloja = :idloja AND iddespesas = :iddespesas');
        end;

        ParamByName('parceladoem').Value := parceladoem;
        ParamByName('datainicio').Value := datainicio;
        ParamByName('valorparcela').Value := valordaparcela;
        ParamByName('despesacadastradapor').Value := despesacadastradapor;
        ParamByName('iddespesas').AsInteger := iddespesas;
        ParamByName('datalancamento').Value := datalancamento;
        ParamByName('valordespesa').Value := valordespesa;
        ParamByName('descricao').Value := descricao;
        ParamByName('comprovante').Value := comprovante;
        ParamByName('observacao').Value := observacao;
        ParamByName('IDCLIENTE').Value := idcliente;
        ParamByName('IDLOJA').Value := idloja;
        ParamByName('periodicidade').Value := periodicidade;
        ParamByName('categoria').Value := categoria;
        ParamByName('DELETADO').Value := 0;
        ParamByName('idempresa').AsInteger := idempresa;
        ParamByName('idpessoa').AsInteger := idpessoa;
        ParamByName('idveiculo').AsInteger := idveiculo;
        ParamByName('periodo').Value := periodo;
        ExecSQL;
      end;

      { ===== Salvar rateios (usando idgeral para bater com FK) ===== }
      // Precisamos do idgeral associado a este (idcliente, idloja, iddespesas)
      LIdGeral := BuscarIdGeralAtual;
      if LIdGeral <= 0 then
        raise Exception.Create('Não foi possível localizar idgeral da despesa para salvar o rateio.');

      // grava rateios (DELETE + INSERT) para o idgeral
      SalvarRateiosOrThrow(LIdGeral);

      // ===== Histórico (apenas em insert), mantido do seu código =====
      if IsInsert then
      begin
        qHist := TFDQuery.Create(nil);
        try
          qHist.Connection := FConn;
          qHist.SQL.Text :=
            'SELECT v.placa, v.iniciolocacao, v.fimlocacao, v.categoria, v.periodicidade, e.nome as empresa ' +
            'FROM gesveiculos v ' +
            'LEFT JOIN gesempresas e ON e.idempresa = v.idempresa ' +
            'WHERE v.idveiculo = :idveiculo';
          qHist.ParamByName('idveiculo').AsInteger := idveiculo;
          qHist.Open;
          if not qHist.IsEmpty then
          begin
            qInsert := TFDQuery.Create(nil);
            try
              qInsert.Connection := FConn;
              qInsert.SQL.Text := 'INSERT INTO historicoveiculo (' +
                'iniciolocacaohistorico, fimlocacaohistorico, valordespesa, descricao,placa, empresa, funcionario, categoria, periodicidade) ' +
                'VALUES (:iniciolocacaohistorico, :fimlocacaohistorico, :valordespesa, :descricao, :placa, :empresa, :funcionario, :categoria, :periodicidade)';
              qInsert.ParamByName('iniciolocacaohistorico').DataType := ftDate;
              qInsert.ParamByName('iniciolocacaohistorico').Value := qHist.FieldByName('iniciolocacao').Value;
              qInsert.ParamByName('fimlocacaohistorico').DataType := ftDate;

              qInsert.ParamByName('fimlocacaohistorico').Value := qHist.FieldByName('fimlocacao').Value;
              qInsert.ParamByName('valordespesa').Value := valordespesa;
              qInsert.ParamByName('descricao').DataType := ftString;
              if Trim(descricao) <> '' then
              begin
                qInsert.ParamByName('descricao').Value := descricao;
              end;

              qInsert.ParamByName('placa').Value := qHist.FieldByName('placa').AsString;
              qInsert.ParamByName('empresa').Value := qHist.FieldByName('empresa').AsString;
              qInsert.ParamByName('funcionario').DataType := ftString;
              qInsert.ParamByName('funcionario').Clear;

              qInsert.ParamByName('categoria').Value := qHist.FieldByName('categoria').AsString;
              qInsert.ParamByName('periodicidade').Value := qHist.FieldByName('periodicidade').AsString;
              qInsert.ExecSQL;
            finally
              qInsert.Free;
            end;
          end;
        finally
          qHist.Free;
        end;
      end;

      erro := '';
      FConn.Commit;
      result := true;
    except
      on ex: exception do
      begin
        erro := 'Erro ao cadastrar despesas: ' + ex.Message;
        FConn.Rollback;
        Result := false;
      end;
    end;
  finally
    qry.Free;
  end;
end;

function TDespesas.Lista(const AQuery: TDictionary<string, string>; out erro: string): TFDQuery;
var
  qry: TFDQuery;
begin
  try
    qry := TFDQuery.Create(nil);
    qry.connection := FConn;
    with qry do
    begin
      Active := false;
      SQL.Clear;
      SQL.Add('Select ');
      SQL.Add('gesdespesas.iddespesas as id, ');
      SQL.Add('DATE_FORMAT(gesdespesas.datalancamento,''%d/%m/%Y'') as datalancamento, ');
      SQL.Add('DATE_FORMAT(gesdespesas.datadocadastro,''%d/%m/%Y'') as datadocadastro, ');
      SQL.Add('DATE_FORMAT(gesdespesas.datainicio,''%d/%m/%Y'') as datainicio, ');
      SQL.Add('gesdespesas.valordespesa, ');
      SQL.Add('gesdespesas.valorparcela, ');
      SQL.Add('gesdespesas.descricao, ');
      SQL.Add('gesdespesas.comprovante, ');
      SQL.Add('gesdespesas.observacao, ');
      SQL.Add('gesdespesas.idcliente, ');
      SQL.Add('gesdespesas.idloja, ');
      SQL.Add('gesdespesas.despesacadastradapor, ');
      SQL.Add('gesdespesas.parceladoem, ');
      SQL.Add('gesdespesas.categoria, ');
      SQL.Add('gesdespesas.periodicidade, ');
      SQL.Add('gesempresas.nome as empresa, ');
      SQL.Add('gespessoa.nome as funcionario, ');
      SQL.Add('gesveiculos.modelo as veiculo, ');
      SQL.Add('gesveiculos.placa');
      SQL.Add('FROM gesdespesas ');
      SQL.Add('LEFT JOIN gesveiculos ON gesveiculos.idveiculo = gesdespesas.idveiculo ');
      SQL.Add('LEFT JOIN gespessoa ON gesdespesas.idpessoa = gespessoa.idpessoa ');
      SQL.Add('LEFT JOIN gesempresas ON gesempresas.idempresa = gesdespesas.idempresa WHERE gesdespesas.iddespesas is not null ');

      //pesquisar
      if AQuery.ContainsKey('busca') then
      begin
        if Length(AQuery.Items['busca']) > 0 then
        begin
          SQL.Add('AND (gesdespesas.datalancamento LIKE ''%' + AQuery.Items['busca'] + '%'' ');
          SQL.Add('OR gesdespesas.valordespesa LIKE ''%' + AQuery.Items['busca'] + '%'' ');
          SQL.Add('OR gesdespesas.comprovante LIKE ''%' + AQuery.Items['busca'] + '%'' )');
        end;
      end;

      if AQuery.ContainsKey('deletado') then
      begin
        if Length(AQuery.Items['deletado']) > 0 then
        begin
          SQL.Add('AND gesdespesas.deletado = :deletado');
          ParamByName('deletado').Value := AQuery.Items['deletado'].ToInteger;
        end;
      end;
      if AQuery.ContainsKey('idcliente') then
      begin
        if Length(AQuery.Items['idcliente']) > 0 then
        begin
          SQL.Add('AND gesdespesas.idcliente = :idcliente');
          ParamByName('idcliente').Value := AQuery.Items['idcliente'].ToInteger;
        end
        else
        begin
          SQL.Add('AND gesdespesas.idcliente = :idcliente');
          ParamByName('idcliente').Value := 0;
        end;
      end;
      if AQuery.ContainsKey('idloja') then
      begin
        if Length(AQuery.Items['idloja']) > 0 then
        begin
          SQL.Add('AND gesdespesas.idloja = :idloja');
          ParamByName('idloja').Value := AQuery.Items['idloja'].ToInteger;
        end
        else
        begin
          SQL.Add('AND gesdespesas.idloja = :idloja');
          ParamByName('idloja').Value := 0;
        end;
      end;
      if AQuery.ContainsKey('idveiculo') then
      begin
        if Length(AQuery.Items['idveiculo']) > 0 then
        begin
          SQL.Add('AND gesdespesas.idveiculo = :idveiculo');
          ParamByName('idveiculo').Value := AQuery.Items['idveiculo'].ToInteger;
        end;
      end;
      SQL.Add('order by id ');
      Active := true;
    end;
    erro := '';
    Result := qry;
  except
    on ex: exception do
    begin
      erro := 'Erro ao consultar : ' + ex.Message;
      Result := nil;
    end;
  end;
end;

function TDespesas.Listaid(const AQuery: TDictionary<string, string>; out erro: string): TFDQuery;
var
  qry: TFDQuery;
  a: integer;
begin
  try
    qry := TFDQuery.Create(nil);
    qry.connection := FConn;
    with qry do
    begin
      Active := false;
      SQL.Clear;
      SQL.Add('Select ');
      SQL.Add('gesdespesas.*, ');
      SQL.Add('gesempresas.nome as empresa, ');
      SQL.Add('gespessoa.nome as funcionario, ');
      SQL.Add('gesveiculos.modelo as veiculo ');
      SQL.Add('From ');
      SQL.Add('gesdespesas left Join ');
      SQL.Add('gesempresas On gesempresas.idempresa = gesdespesas.idempresa left Join ');
      SQL.Add('gesveiculos On gesveiculos.idveiculo = gesdespesas.idveiculo left join gespessoa on gesdespesas.idpessoa = gespessoa.idpessoa');
      SQL.Add('WHERE gesdespesas.idveiculo is not null and gesdespesas.iddespesas=:id ');
      ParamByName('id').AsInteger := AQuery.Items['idpessoabusca'].ToInteger;
      a := AQuery.Items['idpessoabusca'].ToInteger;
      if AQuery.ContainsKey('deletado') then
      begin
        if Length(AQuery.Items['deletado']) > 0 then
        begin
          SQL.Add('AND gesdespesas.deletado = :deletado');
          ParamByName('deletado').Value := AQuery.Items['deletado'].ToInteger;
        end;
      end;
      Active := true;
    end;
    erro := '';
    Result := qry;
  except
    on ex: exception do
    begin
      erro := 'Erro ao consultar : ' + ex.Message;
      Result := nil;
    end;
  end;
end;

end.

